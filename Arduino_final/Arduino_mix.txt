#include <SPI.h>
#include <WiFiNINA.h>

// --- Konstanty ---
const char ssid_binar[] = "Zizice_doma";                      /// @brief SSID WiFi sítě
const char pass_binar[] = "Pavel_Olinka";                     /// @brief Heslo k WiFi síti
const int tcpPort_binar = 8888;                               /// @brief TCP port pro server
const int refreshRateHz_binar = 1000;                         /// @brief Frekvence aktualizace v Hz
const int refreshRate_binar = 1000 / refreshRateHz_binar;     /// @brief Doba mezi aktualizacemi v ms
const int debugPin_binar = 7;                                 /// @brief Pin pro aktivaci debug výpisu
const int serialPrintPin_binar = 6;                           /// @brief Pin pro výpis zasílaných zpráv přes Serial
const int serialBaudRate_binar = 9600;                        /// @brief Baud rate pro Serial komunikaci
const int maxSensors_binar = 4;                               /// @brief Maximální počet podporovaných EMG senzorů
const int emgPins_binar[maxSensors_binar] = {A0, A1, A2, A3}; /// @brief Analogové piny pro senzory
const uint16_t aliveIntervalMs_binar = 2000;                  /// @brief Interval mezi ALIVE zprávami v ms

/// @brief Vypíše debug zprávu do Serial monitoru, pokud je pin v LOW.
/// @param message Zpráva k vypsání
/// @param pin Pin, který aktivuje debug výpis (LOW = aktivní)
void printIfPinLow_binar(const char *message, int pin)
{
    if (digitalRead(pin) == LOW)
    {
        Serial.println(message);
    }
}

/// @brief Třída pro jeden EMG senzor, včetně výpočtu obálky a kalibrace
class EMGSensor_binar
{
private:
    int pin;
    float alpha = 0.6;
    float envelope = 0.0;
    float thresholdUpper = 0.2;
    float thresholdLower = 0.05;
    float mean = 0.0;

public:
    /// @brief Konstruktor EMG senzoru
    /// @param analogPin Analogový pin, ke kterému je senzor připojen
    EMGSensor_binar(int analogPin) : pin(analogPin)
    {
        pinMode(pin, INPUT);
    }

    /// @brief Čtení napětí ze senzoru
    /// @param referenceVoltage Referenční napětí
    /// @param adcResolution Rozlišení ADC převodníku
    /// @return Napětí v rozsahu 0 - referenceVoltage
    float readVoltage(float referenceVoltage = 5.0, int adcResolution = 1023)
    {
        return analogRead(pin) * (referenceVoltage / adcResolution);
    }

    /// @brief Aktualizace obálky signálu pomocí EMA filtru
    /// @param referenceVoltage Referenční napětí
    /// @return Nová hodnota obálky
    float updateEnvelope(float referenceVoltage = 5.0)
    {
        float voltage = readVoltage(referenceVoltage);
        float centered = voltage - (referenceVoltage / 3.0);
        float rectified = fabs(centered);
        envelope = alpha * rectified + (1 - alpha) * envelope;
        return envelope;
    }

    /// @brief Zjištění, zda je aktivita nad prahem
    /// @return True, pokud obálka překročila práh
    bool isActive() const { return envelope > thresholdUpper; }

    /// @brief Kalibrace senzoru založená na průměru a směrodatné odchylce
    /// @param durationMs Doba kalibrace v milisekundách
    void calibrate(unsigned long durationMs = 3000)
    {
        const int maxSamples = durationMs / refreshRate_binar;

        float samples[maxSamples];
        int count = 0;

        for (unsigned long tStart = millis(); millis() - tStart < durationMs; delay(refreshRate_binar))
        {
            updateEnvelope();
            if (count < maxSamples)
            {
                samples[count++] = envelope;
            }
        }

        float sum = 0;
        for (int i = 0; i < count; i++)
            sum += samples[i];
        mean = sum / count;

        float varSum = 0;
        for (int i = 0; i < count; i++)
            varSum += pow(samples[i] - mean, 2);
        float stdDev = sqrt(varSum / count);

        thresholdUpper = mean + 3.0 * stdDev;
        thresholdLower = mean - 3.0 * stdDev;

        printIfPinLow_binar("Kalibrace:", debugPin_binar);

        char buf[50];
        char floatStr[20];

        dtostrf(mean, 7, 4, floatStr); // šířka 7 znaků, 4 desetinná místa
        snprintf(buf, sizeof(buf), "Průměr: %s", floatStr);
        printIfPinLow_binar(buf, debugPin_binar);

        dtostrf(stdDev, 7, 4, floatStr);
        snprintf(buf, sizeof(buf), "Směrodatná odchylka: %s", floatStr);
        printIfPinLow_binar(buf, debugPin_binar);

        dtostrf(thresholdUpper, 7, 4, floatStr);
        snprintf(buf, sizeof(buf), "Nastaven prah upper: %s", floatStr);
        printIfPinLow_binar(buf, debugPin_binar);

        dtostrf(thresholdLower, 7, 4, floatStr);
        snprintf(buf, sizeof(buf), "Nastaven prah lower: %s", floatStr);
        printIfPinLow_binar(buf, debugPin_binar);
    }

    /// @brief Vrací aktuální obálku signálu
    /// @return Poslední hodnota obálky
    float getEnvelope() const
    {
        return envelope;
    }

    /// @brief Nastaví nový práh pro detekci aktivity
    /// @param t Nová hodnota prahu
    void setThreshold(float tu, float tl)
    {
        thresholdUpper = tu;
        thresholdLower = tl;
    }
};

/// @brief Třída reprezentující celý systém EMG senzorů a TCP komunikace
class EMGSystem_binar
{
private:
    EMGSensor_binar *sensors[maxSensors_binar];
    int sensorCount;
    WiFiServer server;
    WiFiClient client;
    bool initialized = false;
    long lastAliveTime = 0;
    int lastSentValue = -1;
    int currentWindowValue = 0;
    unsigned long windowStartTime = 0;
    const unsigned long sendDelayMs = 1500;
    bool shouldSendZero = false;

    /// @brief Přijímá nového klienta a inicializuje senzory podle vstupu
    void handleNewClient()
    {
        client = server.available();
        if (!client)
            return;

        printIfPinLow_binar("Klient připojen, čekám na počet senzorů...", debugPin_binar);

        unsigned long connectTime = millis(); // čas připojení
        char input[10] = {0};

        // Čekej max. 3 sekundy na vstup
        while (millis() - connectTime < 3000)
        {
            if (readClientInput(input, sizeof(input)))
            {
                int count = atoi(input);
                if (count >= 1 && count <= maxSensors_binar)
                {
                    initSensors(count);
                    return;
                }
                else
                {
                    printIfPinLow_binar("Neplatný vstup od klienta.", debugPin_binar);
                    client.stop();
                    return;
                }
            }
        }

        // Po 3 vteřinách bez vstupu
        printIfPinLow_binar("Klient neposlal data do 3 vteřin - odpojuji.", debugPin_binar);
        client.stop();
    }

    /// @brief Načte vstup od klienta do bufferu
    /// @param buffer Výstupní buffer
    /// @param bufferSize Velikost bufferu
    /// @param timeoutMs Timeout pro čekání na vstup
    /// @return True pokud byl vstup úspěšně načten
    bool readClientInput(char *buffer, size_t bufferSize, uint16_t timeoutMs = 3000)
    {
        size_t index = 0;
        unsigned long timeout = millis() + timeoutMs;
        while (millis() < timeout)
        {
            if (client.available())
            {
                char c = client.read();
                if (c == '\n' || index >= bufferSize - 1)
                {
                    buffer[index] = '\0';
                    return true;
                }
                buffer[index++] = c;
            }
        }
        buffer[0] = '\0';
        return false;
    }

    /// @brief Odesílá zakódovaná data klientovi, včetně ALIVE zpráv
    void sendDataToClient()
    {
        if (!client || !client.connected())
        {
            printIfPinLow_binar("Klient odpojen během odesílání dat.", debugPin_binar);
            cleanupClient();
            return;
        }

        int sensorData[sensorCount];
        char serialMsg[50] = "";

        for (int i = 0; i < sensorCount; i++)
        {
            sensors[i]->updateEnvelope();
            sensorData[i] = sensors[i]->isActive() ? 1 : 0;

            char buffer[8];
            dtostrf(sensors[i]->getEnvelope(), 6, 4, buffer);
            strcat(serialMsg, buffer);
            if (i < sensorCount - 1)
                strcat(serialMsg, ",");
        }

        // Posílání reálných hodnot z EMG senzorů přes Serial
        printIfPinLow_binar(serialMsg, serialPrintPin_binar);

        if (digitalRead(serialPrintPin_binar) == LOW)
        {
            // Výpis je aktivní na serial, TCP data neposíláme
            return;
        }

        int newValue = 0;
        for (int i = 0; i < sensorCount; i++)
        {
            newValue += sensorData[i] << (sensorCount - 1 - i);
        }

        unsigned long now = millis();

        if (windowStartTime == 0)
        {
            windowStartTime = now;
            currentWindowValue = newValue;
        }

        if (newValue > currentWindowValue)
        {
            currentWindowValue = newValue;
        }

        if (now - windowStartTime >= sendDelayMs)
        {
            if (shouldSendZero)
            {
                client.print("0\n");
                printIfPinLow_binar("0", debugPin_binar);
                shouldSendZero = false;
            }
            else if (currentWindowValue != 0 && currentWindowValue != lastSentValue)
            {
                lastSentValue = currentWindowValue;
                char msg[20];
                snprintf(msg, sizeof(msg), "%d\n", currentWindowValue);
                client.print(msg);
                printIfPinLow_binar(msg, debugPin_binar);
                shouldSendZero = true; // při příštím okně odešli nulu
            }

            windowStartTime = 0;
            currentWindowValue = 0;
        }

        if (now - lastAliveTime >= aliveIntervalMs_binar)
        {
            client.print("ALIVE\n");
            printIfPinLow_binar("Odesláno: ALIVE", debugPin_binar);
            lastAliveTime = now;
        }

        if (!client.connected())
        {
            printIfPinLow_binar("Klient odpojen po odeslání dat.", debugPin_binar);
            cleanupClient();
        }
    }

    /// @brief Vyčištění senzorů
    void cleanupSensors()
    {
        for (int i = 0; i < maxSensors_binar; i++)
        {
            if (sensors[i] != nullptr)
            {
                delete sensors[i];
                sensors[i] = nullptr;
            }
        }
        sensorCount = 0;
        initialized = false;
        printIfPinLow_binar("Senzory byly vyčištěny.", debugPin_binar);
    }

    /// @brief Ukončení spojení s klientem a reset systému
    void cleanupClient()
    {
        client.stop();
        cleanupSensors();
        lastSentValue = -1;
        printIfPinLow_binar("Klient odpojen a systém resetován.", debugPin_binar);
    }

    /// @brief Inicializace senzorů dle zadaného počtu a provedení kalibrace
    /// @param count Počet senzorů
    void initSensors(int count)
    {
        cleanupSensors();

        if (count < 1 || count > maxSensors_binar)
        {
            printIfPinLow_binar("Neplatný počet senzorů, inicializace selhala.", debugPin_binar);
            return;
        }

        sensorCount = count;
        for (int i = 0; i < sensorCount; i++)
        {
            sensors[i] = new EMGSensor_binar(emgPins_binar[i]);
        }

        lastAliveTime = millis();

        printIfPinLow_binar("Spouštím paralelní kalibraci senzorů...", debugPin_binar);

        const int samplingRate = 100;
        const int sampleInterval = 1000 / samplingRate;
        const int maxSamples = 3000 / sampleInterval;

        float samples[maxSensors_binar][maxSamples] = {0};
        int counts[maxSensors_binar] = {0};
        unsigned long tStart = millis();

        while (millis() - tStart < 3000)
        {
            for (int i = 0; i < sensorCount; i++)
            {
                sensors[i]->updateEnvelope();
                if (counts[i] < maxSamples)
                {
                    samples[i][counts[i]++] = sensors[i]->getEnvelope();
                }
            }
            delay(sampleInterval);
        }

        for (int i = 0; i < sensorCount; i++)
        {
            float sum = 0;
            for (int j = 0; j < counts[i]; j++)
                sum += samples[i][j];
            float mean = sum / counts[i];

            float varSum = 0;
            for (int j = 0; j < counts[i]; j++)
                varSum += pow(samples[i][j] - mean, 2);
            float stdDev = sqrt(varSum / counts[i]);

            sensors[i]->setThreshold(mean + 3.0 * stdDev, mean - 3.0 * stdDev);

            char buf[80];
            char meanStr[10], stddevStr[10], thresholdStr[10];
            dtostrf(mean, 6, 4, meanStr);
            dtostrf(stdDev, 6, 4, stddevStr);
            dtostrf(mean + 3.0 * stdDev, 6, 4, thresholdStr);

            snprintf(buf, sizeof(buf), "Senzor %d - průměr: %s, směr. odch.: %s, prah: %s",
                     i, meanStr, stddevStr, thresholdStr);
            printIfPinLow_binar(buf, debugPin_binar);
        }
        printIfPinLow_binar("Paralelní kalibrace dokončena.", debugPin_binar);

        char msg[50];
        snprintf(msg, sizeof(msg), "Inicializováno %d EMG senzor(ů).", sensorCount);
        printIfPinLow_binar(msg, debugPin_binar);
        initialized = true;
    }

public:
    /// @brief Konstruktor EMG systému
    /// @param port TCP port pro server
    EMGSystem_binar(int port) : server(port), sensorCount(0) {}

    /// @brief Zahájení WiFi komunikace a spuštění serveru
    void beginWiFi()
    {
        int status = WL_IDLE_STATUS;
        while (status != WL_CONNECTED)
        {
            char debugMessage[60];
            snprintf(debugMessage, sizeof(debugMessage), "Připojování k SSID: %s", ssid_binar);
            printIfPinLow_binar(debugMessage, debugPin_binar);
            status = WiFi.begin(ssid_binar, pass_binar);
            delay(5000);
        }
        printIfPinLow_binar("WiFi připojeno", debugPin_binar);
        server.begin();
    }

    /// @brief Hlavní smyčka pro obsluhu EMG dat a komunikace
    void update()
    {
        static bool wifiDisconnectedPrinted = false;
        static bool noClientPrinted = false;
        static bool notInitializedPrinted = false;

        if (WiFi.status() != WL_CONNECTED)
        {
            if (!wifiDisconnectedPrinted)
            {
                printIfPinLow_binar("WiFi odpojeno, připojuji znovu...", debugPin_binar);
                wifiDisconnectedPrinted = true;
            }
            beginWiFi();
            return;
        }
        else
        {
            wifiDisconnectedPrinted = false;
        }

        if (!client || !client.connected())
        {
            if (!noClientPrinted)
            {
                printIfPinLow_binar("Žádný klient není připojen.", debugPin_binar);
                noClientPrinted = true;
            }
            handleNewClient();
            return;
        }
        else
        {
            noClientPrinted = false;
        }

        if (!initialized)
        {
            if (!notInitializedPrinted)
            {
                printIfPinLow_binar("Senzory nejsou inicializovány.", debugPin_binar);
                notInitializedPrinted = true;
            }
            return;
        }
        else
        {
            notInitializedPrinted = false;
        }

        sendDataToClient();
    }
};

EMGSystem_binar emgSystem_binar(tcpPort_binar);

void setup_binar()
{
    pinMode(debugPin_binar, INPUT_PULLUP);
    pinMode(serialPrintPin_binar, INPUT_PULLUP);
    Serial.begin(serialBaudRate_binar);
    while (!Serial)
        ;
    printIfPinLow_binar("Start systému...", debugPin_binar);
    emgSystem_binar.beginWiFi();

    IPAddress ip = WiFi.localIP();
    char ipStr[16];
    snprintf(ipStr, sizeof(ipStr), "%d.%d.%d.%d", ip[0], ip[1], ip[2], ip[3]);
    char msg[40];
    snprintf(msg, sizeof(msg), "IP adresa: %s", ipStr);
    Serial.println(msg);
    printIfPinLow_binar("Systém připraven, čekám na klienta...", debugPin_binar);
    while (digitalRead(debugPin_binar) == LOW && digitalRead(serialPrintPin_binar) == LOW) // čeká na aktivaci debug pinu
    {
        Serial.println("Pro správné zasílání dat přes serial je potřeba mít zapojen debug pin (pin 7), nebo serial print pin (pin 6). NE OBOJÍ!");
        delay(1000);
    }
}

void loop_binar()
{
    int timeStart = millis();
    emgSystem_binar.update();
    int timeEnd = millis();
    delay(max(0, refreshRate_binar - (timeEnd - timeStart)));
}

// --- Konstanty ---
const char ssid[] = "Zizice_doma";                /// @brief SSID WiFi sítě
const char pass[] = "Pavel_Olinka";               /// @brief Heslo k WiFi síti
const int tcpPort = 8888;                         /// @brief TCP port pro server
const int refreshRateHz = 1000;                   /// @brief Frekvence aktualizace v Hz
const int refreshRate = 1000 / refreshRateHz;     /// @brief Doba mezi aktualizacemi v ms
const int debugPin = 7;                           /// @brief Pin pro aktivaci debug výpisu
const int serialPrintPin = 6;                     /// @brief Pin pro výpis zasílaných zpráv přes Serial
const int serialBaudRate = 9600;                  /// @brief Baud rate pro Serial komunikaci
const int maxSensors = 4;                         /// @brief Maximální počet podporovaných EMG senzorů
const int emgPins[maxSensors] = {A0, A1, A2, A3}; /// @brief Analogové piny pro senzory
const uint16_t aliveIntervalMs = 2000;            /// @brief Interval mezi ALIVE zprávami v ms
const int maxSendValue = 7;                       /// @brief Maximální hodnota pro odesílání (1-7)

/// @brief Vypíše debug zprávu do Serial monitoru, pokud je pin v LOW.
/// @param message Zpráva k vypsání
/// @param pin Pin, který aktivuje debug výpis (LOW = aktivní)
void printIfPinLow(const char *message, int pin)
{
    if (digitalRead(pin) == LOW)
    {
        Serial.println(message);
    }
}

/// @brief Třída pro jeden EMG senzor, včetně výpočtu obálky a kalibrace
class EMGSensor
{
private:
    int pin;
    float alpha = 0.6;
    float envelope = 0.0;
    float thresholdUpper = 0.2;
    float thresholdLower = 0.05;
    float mean = 0.0;

public:
    /// @brief Konstruktor EMG senzoru
    /// @param analogPin Analogový pin, ke kterému je senzor připojen
    EMGSensor(int analogPin) : pin(analogPin)
    {
        pinMode(pin, INPUT);
    }

    /// @brief Čtení napětí ze senzoru
    /// @param referenceVoltage Referenční napětí
    /// @param adcResolution Rozlišení ADC převodníku
    /// @return Napětí v rozsahu 0 - referenceVoltage
    float readVoltage(float referenceVoltage = 5.0, int adcResolution = 1023)
    {
        return analogRead(pin) * (referenceVoltage / adcResolution);
    }

    /// @brief Aktualizace obálky signálu pomocí EMA filtru
    /// @param referenceVoltage Referenční napětí
    /// @return Nová hodnota obálky
    float updateEnvelope(float referenceVoltage = 5.0)
    {
        float voltage = readVoltage(referenceVoltage);
        float centered = voltage - (referenceVoltage / 3.0);
        float rectified = fabs(centered);
        envelope = alpha * rectified + (1 - alpha) * envelope;
        return envelope;
    }

    /// @brief Zjištění, zda je aktivita nad prahem
    /// @return True, pokud obálka překročila práh
    bool isActive() const { return envelope > thresholdUpper; }

    /// @brief Kalibrace senzoru založená na průměru a směrodatné odchylce
    /// @param durationMs Doba kalibrace v milisekundách
    void calibrate(unsigned long durationMs = 3000)
    {
        const int maxSamples = durationMs / refreshRate;

        float samples[maxSamples];
        int count = 0;

        for (unsigned long tStart = millis(); millis() - tStart < durationMs; delay(refreshRate))
        {
            updateEnvelope();
            if (count < maxSamples)
            {
                samples[count++] = envelope;
            }
        }

        float sum = 0;
        for (int i = 0; i < count; i++)
            sum += samples[i];
        mean = sum / count;

        float varSum = 0;
        for (int i = 0; i < count; i++)
            varSum += pow(samples[i] - mean, 2);
        float stdDev = sqrt(varSum / count);

        thresholdUpper = mean + 3.0 * stdDev;
        thresholdLower = mean - 3.0 * stdDev;

        printIfPinLow("Kalibrace:", debugPin);

        char buf[50];
        char floatStr[20];

        dtostrf(mean, 7, 4, floatStr);
        snprintf(buf, sizeof(buf), "Průměr: %s", floatStr);
        printIfPinLow(buf, debugPin);

        dtostrf(stdDev, 7, 4, floatStr);
        snprintf(buf, sizeof(buf), "Směrodatná odchylka: %s", floatStr);
        printIfPinLow(buf, debugPin);

        dtostrf(thresholdUpper, 7, 4, floatStr);
        snprintf(buf, sizeof(buf), "Nastaven prah upper: %s", floatStr);
        printIfPinLow(buf, debugPin);

        dtostrf(thresholdLower, 7, 4, floatStr);
        snprintf(buf, sizeof(buf), "Nastaven prah lower: %s", floatStr);
        printIfPinLow(buf, debugPin);
    }

    /// @brief Vrací aktuální obálku signálu
    /// @return Poslední hodnota obálky
    float getEnvelope() const
    {
        return envelope;
    }

    /// @brief Nastaví nový práh pro detekci aktivity
    /// @param t Nová hodnota prahu
    void setThreshold(float tu, float tl)
    {
        thresholdUpper = tu;
        thresholdLower = tl;
    }
};

/// @brief Třída reprezentující celý systém EMG pro cyklické a odesílací ovládání
class EMGSystem
{
private:
    EMGSensor *sensors[2]; // Používáme pouze dva senzory (A0, A1)
    WiFiServer server;
    WiFiClient client;
    bool initialized = false;
    long lastAliveTime = 0;
    int cycledValue = 1; // aktuálně navolené číslo (1-7)
    bool emg1LastActive = false;
    bool emg2LastActive = false;

    void sendAliveIfNeeded()
    {
        unsigned long now = millis();
        if (now - lastAliveTime >= aliveIntervalMs)
        {
            client.print("ALIVE\n");
            printIfPinLow("Odesláno: ALIVE", debugPin);
            lastAliveTime = now;
        }
    }

    void handleLogic()
    {
        sensors[0]->updateEnvelope(); // První EMG (cyklování)
        sensors[1]->updateEnvelope(); // Druhý EMG (odesílání)

        bool emg1Active = sensors[0]->isActive();
        bool emg2Active = sensors[1]->isActive();

        // Pokud první EMG bylo neaktivní a nyní je aktivní → posuň číslo
        if (emg1Active && !emg1LastActive)
        {
            cycledValue++;
            if (cycledValue > maxSendValue)
                cycledValue = 1;

            char msg[32];
            snprintf(msg, sizeof(msg), "Navoleno: %d", cycledValue);
            printIfPinLow(msg, debugPin);
        }

        // Pokud druhý EMG byl neaktivní a nyní je aktivní → odešli aktuální hodnotu
        if (emg2Active && !emg2LastActive)
        {
            char msg[16];
            snprintf(msg, sizeof(msg), "%d\n", cycledValue);
            client.print(msg);
            printIfPinLow(msg, debugPin);
        }

        // Update stavu pro další smyčku
        emg1LastActive = emg1Active;
        emg2LastActive = emg2Active;

        // Posílej také stav obou EMG na Serial (pro vizualizaci)
        if (digitalRead(serialPrintPin) == LOW)
        {
            char serialMsg[50];
            dtostrf(sensors[0]->getEnvelope(), 6, 4, serialMsg);
            strcat(serialMsg, ",");
            char buf[20];
            dtostrf(sensors[1]->getEnvelope(), 6, 4, buf);
            strcat(serialMsg, buf);
            printIfPinLow(serialMsg, serialPrintPin);
        }
    }

    void calibrateSensors()
    {
        for (int i = 0; i < 2; i++)
        {
            sensors[i]->calibrate();
        }
        initialized = true;
    }

    void initSensors()
    {
        cleanupSensors();
        for (int i = 0; i < 2; i++)
        {
            sensors[i] = new EMGSensor(emgPins[i]);
        }
        calibrateSensors();
        cycledValue = 1;
        printIfPinLow("Systém inicializován pro 2 EMG senzory.", debugPin);
    }

    void handleNewClient()
    {
        client = server.available();
        if (!client)
            return;
        printIfPinLow("Klient připojen - inicializuji senzory", debugPin);
        initSensors();
    }

    void cleanupSensors()
    {
        for (int i = 0; i < 2; i++)
        {
            if (sensors[i] != nullptr)
            {
                delete sensors[i];
                sensors[i] = nullptr;
            }
        }
        initialized = false;
    }

    void cleanupClient()
    {
        client.stop();
        cleanupSensors();
        printIfPinLow("Klient odpojen a systém resetován.", debugPin);
    }

public:
    EMGSystem(int port) : server(port) {}

    void beginWiFi()
    {
        int status = WL_IDLE_STATUS;
        while (status != WL_CONNECTED)
        {
            char debugMessage[60];
            snprintf(debugMessage, sizeof(debugMessage), "Připojování k SSID: %s", ssid);
            printIfPinLow(debugMessage, debugPin);
            status = WiFi.begin(ssid, pass);
            delay(5000);
        }
        printIfPinLow("WiFi připojeno", debugPin);
        server.begin();
    }

    void update()
    {
        static bool wifiDisconnectedPrinted = false;
        static bool noClientPrinted = false;
        static bool notInitializedPrinted = false;

        if (WiFi.status() != WL_CONNECTED)
        {
            if (!wifiDisconnectedPrinted)
            {
                printIfPinLow("WiFi odpojeno, připojuji znovu...", debugPin);
                wifiDisconnectedPrinted = true;
            }
            beginWiFi();
            return;
        }
        else
        {
            wifiDisconnectedPrinted = false;
        }

        if (!client || !client.connected())
        {
            if (!noClientPrinted)
            {
                printIfPinLow("Žádný klient není připojen.", debugPin);
                noClientPrinted = true;
            }
            handleNewClient();
            return;
        }
        else
        {
            noClientPrinted = false;
        }

        if (!initialized)
        {
            if (!notInitializedPrinted)
            {
                printIfPinLow("Senzory nejsou inicializovány.", debugPin);
                notInitializedPrinted = true;
            }
            return;
        }
        else
        {
            notInitializedPrinted = false;
        }

        handleLogic();
        sendAliveIfNeeded();

        if (!client.connected())
        {
            printIfPinLow("Klient odpojen po odeslání dat.", debugPin);
            cleanupClient();
        }
    }
};

EMGSystem emgSystem(tcpPort);

void setup_cycle(){
    pinMode(debugPin, INPUT_PULLUP);
    pinMode(serialPrintPin, INPUT_PULLUP);
    Serial.begin(serialBaudRate);
    while (!Serial)
        ;
    printIfPinLow("Start systému...", debugPin);
    emgSystem.beginWiFi();

    IPAddress ip = WiFi.localIP();
    char ipStr[16];
    snprintf(ipStr, sizeof(ipStr), "%d.%d.%d.%d", ip[0], ip[1], ip[2], ip[3]);
    char msg[40];
    snprintf(msg, sizeof(msg), "IP adresa: %s", ipStr);
    Serial.println(msg);
    printIfPinLow("Systém připraven, čekám na klienta...", debugPin);

    while (digitalRead(debugPin) == LOW && digitalRead(serialPrintPin) == LOW)
    {
        Serial.println("Pro správné zasílání dat přes serial je potřeba mít zapojen debug pin (pin 7), nebo serial print pin (pin 6). NE OBOJÍ!");
        delay(1000);
    }
}

void loop_cycle()
{
    int timeStart = millis();
    emgSystem.update();
    int timeEnd = millis();
    delay(max(0, refreshRate - (timeEnd - timeStart)));
}

void (*chosen_update)();

/// @brief Inicializace systému při startu
void setup()
{
    pinMode(13, INPUT_PULLDOWN);
    if (digitalRead(13) == LOW)
    {
        // Pokud je pin 13 v LOW, spustí se setup_binar
        setup_binar();
        chosen_update = loop_binar;
    }
    else
    {
        setup_cycle();
        chosen_update = loop_cycle;
    }
}

/// @brief Hlavní smyčka programu
void loop()
{
    chosen_update();
}
